uniform sampler2D uProfile;
uniform sampler2D uNoiseTex;
uniform float uUseNoiseTex;
uniform float uProfileIsImage;
uniform sampler2D uCurveTex;
uniform float uHeight;
uniform float uTime;
uniform float uScroll;
uniform float uFrequency;
uniform float uOctaves;
uniform float uGain;
uniform float uLacunarity;
uniform float uStability;
uniform float uIntensity;
uniform float uSliceCount;
uniform vec3 uTint;
uniform vec3 uNoiseOffset;

varying vec3 v2f_worldPos;

// classic 3D Perlin (Stefan Gustavson / Ashima Arts, MIT)
vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec4 permute(vec4 x) { return mod289(((x * 34.0) + 1.0) * x); }
vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
vec3 fade(vec3 t) { return t * t * t * (t * (t * 6.0 - 15.0) + 10.0); }

float cnoise(vec3 P) {
    vec3 Pi0 = floor(P);
    vec3 Pi1 = Pi0 + vec3(1.0);
    Pi0 = mod289(Pi0);
    Pi1 = mod289(Pi1);
    vec3 Pf0 = fract(P);
    vec3 Pf1 = Pf0 - vec3(1.0);
    vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
    vec4 iy = vec4(Pi0.yy, Pi1.yy);
    vec4 iz0 = Pi0.zzzz;
    vec4 iz1 = Pi1.zzzz;

    vec4 ixy = permute(permute(ix) + iy);
    vec4 ixy0 = permute(ixy + iz0);
    vec4 ixy1 = permute(ixy + iz1);

    vec4 gx0 = ixy0 * (1.0 / 7.0);
    vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
    gx0 = fract(gx0);
    vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
    vec4 sz0 = step(gz0, vec4(0.0));
    gx0 -= sz0 * (step(0.0, gx0) - 0.5);
    gy0 -= sz0 * (step(0.0, gy0) - 0.5);

    vec4 gx1 = ixy1 * (1.0 / 7.0);
    vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
    gx1 = fract(gx1);
    vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
    vec4 sz1 = step(gz1, vec4(0.0));
    gx1 -= sz1 * (step(0.0, gx1) - 0.5);
    gy1 -= sz1 * (step(0.0, gy1) - 0.5);

    vec3 g000 = vec3(gx0.x, gy0.x, gz0.x);
    vec3 g100 = vec3(gx0.y, gy0.y, gz0.y);
    vec3 g010 = vec3(gx0.z, gy0.z, gz0.z);
    vec3 g110 = vec3(gx0.w, gy0.w, gz0.w);
    vec3 g001 = vec3(gx1.x, gy1.x, gz1.x);
    vec3 g101 = vec3(gx1.y, gy1.y, gz1.y);
    vec3 g011 = vec3(gx1.z, gy1.z, gz1.z);
    vec3 g111 = vec3(gx1.w, gy1.w, gz1.w);

    vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
    g000 *= norm0.x;
    g010 *= norm0.y;
    g100 *= norm0.z;
    g110 *= norm0.w;
    vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
    g001 *= norm1.x;
    g011 *= norm1.y;
    g101 *= norm1.z;
    g111 *= norm1.w;

    float n000 = dot(g000, Pf0);
    float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
    float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
    float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
    float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
    float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
    float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
    float n111 = dot(g111, Pf1);

    vec3 fade_xyz = fade(Pf0);
    vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
    vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
    float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
    return 2.2 * n_xyz;
}

// nzw.png をハッシュに使う勾配ノイズ
vec3 hashTex(vec3 cell) {
    vec2 uv = (cell.xy + vec2(37.0, 17.0) * cell.z + 0.5) / 128.0;
    return texture2D(uNoiseTex, uv).rgb * 2.0 - 1.0;
}

float tnoise(vec3 p) {
    vec3 i = floor(p);
    vec3 f = fract(p);
    vec3 w = f * f * (3.0 - 2.0 * f);
    float n000 = dot(hashTex(i + vec3(0.0, 0.0, 0.0)), f - vec3(0.0, 0.0, 0.0));
    float n100 = dot(hashTex(i + vec3(1.0, 0.0, 0.0)), f - vec3(1.0, 0.0, 0.0));
    float n010 = dot(hashTex(i + vec3(0.0, 1.0, 0.0)), f - vec3(0.0, 1.0, 0.0));
    float n110 = dot(hashTex(i + vec3(1.0, 1.0, 0.0)), f - vec3(1.0, 1.0, 0.0));
    float n001 = dot(hashTex(i + vec3(0.0, 0.0, 1.0)), f - vec3(0.0, 0.0, 1.0));
    float n101 = dot(hashTex(i + vec3(1.0, 0.0, 1.0)), f - vec3(1.0, 0.0, 1.0));
    float n011 = dot(hashTex(i + vec3(0.0, 1.0, 1.0)), f - vec3(0.0, 1.0, 1.0));
    float n111 = dot(hashTex(i + vec3(1.0, 1.0, 1.0)), f - vec3(1.0, 1.0, 1.0));
    vec4 nz = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), w.z);
    vec2 nyz = mix(nz.xy, nz.zw, w.y);
    return mix(nyz.x, nyz.y, w.x) * 1.6;
}

float noise3(vec3 p) {
    if (uUseNoiseTex > 0.5) return tnoise(p);
    return cnoise(p);
}

float turbulence(vec3 p) {
    float sum = 0.0;
    float ampSum = 0.0;
    float amp = 1.0;
    vec3 pp = p;
    for (int i = 0; i < 4; i++) {
        float w = clamp(uOctaves - float(i), 0.0, 1.0);
        sum += noise3(pp) * amp * w;
        ampSum += amp * w;
        amp *= uGain;
        pp *= uLacunarity;
    }
    // ±1 程度に正規化 (振幅が大きいと v の勾配が反転して白い塊が出る)
    return sum / max(ampSum, 1e-4);
}

void main() {
    float h = v2f_worldPos.y / uHeight;
    if (h < 0.0 || h > 1.0) discard;

    vec4 curve = texture2D(uCurveTex, vec2(h, 0.5));
    vec2 xy = (v2f_worldPos.xz - curve.xy) / curve.z;
    float u = length(xy);
    if (u > 1.35) discard;

    // -time で上昇流
    vec3 npos = vec3(v2f_worldPos.x - curve.x, v2f_worldPos.y - uTime * uScroll, v2f_worldPos.z - curve.y)
              * uFrequency + uNoiseOffset;
    float turb = turbulence(npos);

    float v = h + uStability * sqrt(max(h, 0.0)) * turb;

    vec4 col = texture2D(uProfile, vec2(clamp(u, 0.0, 1.0), clamp(v, 0.0, 1.0)));
    if (uProfileIsImage > 0.5) {
        // αなし画像は輝度を不透明度に
        col.a = clamp(max(col.r, max(col.g, col.b)) * 1.5, 0.0, 1.0);
    }

    // ボリューム上端で切れた塊が浮かないよう先端をフェード
    float tip = smoothstep(1.0, 0.78, h);
    col.a *= tip * tip;
    // 下に引かれたルックアップ(浮いた高温部)は冷却として減光
    col.a *= exp(-5.0 * max(h - v, 0.0));
    vec3 rgb = col.rgb * uTint;

    float w = uIntensity * (42.0 / uSliceCount);
    gl_FragColor = vec4(rgb * col.a * w, 1.0);
    #include <tonemapping_fragment>
    #include <colorspace_fragment>
}
